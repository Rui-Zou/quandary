package parser;

import java.util.*;
import java.io.*;
import java_cup.runtime.Symbol;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.ComplexSymbolFactory;

import ast.*;
import interpreter.Interpreter;

parser code {: 
  Lexer lexer;

  public Parser(Lexer lex, ComplexSymbolFactory sf) {
    super(lex, sf);
    lexer = lex;
  }

  public void report_fatal_error(String message, Object info) {
    Interpreter.fatalError(message + " (" + info + ")", Interpreter.EXIT_PARSING_ERROR);
  }

  ast.Location loc(int line, int column) {
    return new ast.Location(line, column);
  }

:};

terminal PLUS, MINUS, TIMES, LPAREN, RPAREN;
terminal UMINUS;
terminal Long    INTCONST;
terminal RETURN, SEMICOLON;

non terminal Program         program;
non terminal Statement       statement;
non terminal Expr            expr;
non terminal BinaryExpr      binaryExpr;
non terminal UnaryExpr       unaryExpr;

precedence left PLUS, MINUS;
precedence left TIMES;
precedence left UMINUS;

start with program;

program ::= statement:s
            {: RESULT = new Program(s, loc(sleft, sright)); :}
         ;

statement ::= expr:e
                {: RESULT = new Statement("", e, loc(eleft, eright)); :}
            | RETURN expr:e SEMICOLON
                {: RESULT = new Statement("return", e, loc(eleft, eright)); :}
            ;

expr ::= INTCONST:c
         {: RESULT = new ConstExpr(c, loc(cleft, cright)); :}
      |  MINUS expr:e
         {: RESULT = new UnaryExpr(e, loc(eleft, eright)); :}
         %prec UMINUS
      |  binaryExpr:e
         {: RESULT = e; :}
      |  LPAREN expr:e RPAREN
         {: RESULT = e; :}
      ;

binaryExpr ::= expr:e1 PLUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.PLUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 MINUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.MINUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 TIMES expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.TIMES, e2, loc(e1left, e1right)); :}
            ;